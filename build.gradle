import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.19'
    id 'com.palantir.git-version' version '3.1.0'
    id 'pmd'
}

bootJar {
    archiveFileName = 'app.jar'
}

jar {
    enabled = false
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
}

dependencies {
    implementation 'io.micrometer:micrometer-tracing-bridge-brave:1.3.1'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-config-client'
    implementation 'org.springframework.cloud:spring-cloud-starter'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.springframework.hateoas:spring-hateoas:2.3.0'

    compileOnly 'org.projectlombok:lombok'
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"

    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.apache.commons:commons-lang3:3.14.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

def details = versionDetails()

springBoot {
    buildInfo {
        group = "com.buddy.api"
        version = details.lastTag ?: "0.0.0"
        properties {
            additional = ['developers': 'hywenklis.']
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

tasks.jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    maxWarnings = 0
    showViolations = true
    ignoreFailures = false
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile('config/checkstyle/xsl/checkstyle-custom.xsl')
    }
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyleMain {
    dependsOn compileJava
}

checkstyleTest {
    dependsOn compileTestJava
}

spotbugs {
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
    effort = Effort.MAX
    reportLevel = Confidence.valueOf('HIGH')
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

pmd {
    toolVersion = "7.0.0"
    ignoreFailures = false
    consoleOutput = true
}
