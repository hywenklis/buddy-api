import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.23'
    id 'com.palantir.git-version' version '3.1.0'
    id 'pmd'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
    set('mapstructVersion', "1.5.5.Final")
    set('springdocOpenApiVersion', "2.6.0")
    set('springHateoasVersion', "2.3.0")
    set('commonsLang3Version', "3.14.0")
    set('lombokVersion', "1.18.30")
    set('postgresqlVersion', "42.7.2")
    set('h2Version', "2.2.224")
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-config-client'
    implementation 'org.springframework.cloud:spring-cloud-starter'

    // Outras dependÃªncias
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocOpenApiVersion}"
    implementation "org.springframework.hateoas:spring-hateoas:${springHateoasVersion}"
    implementation 'io.micrometer:micrometer-tracing-bridge-brave:1.3.4'
    implementation 'org.flywaydb:flyway-core'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    runtimeOnly 'org.flywaydb:flyway-database-postgresql:10.18.2'
    runtimeOnly "org.postgresql:postgresql:${postgresqlVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "com.h2database:h2:${h2Version}"
    testImplementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootJar {
    archiveFileName = 'app.jar'
}

jar {
    enabled = false
}

def details = versionDetails()

springBoot {
    buildInfo {
        properties {
            group = "com.buddy.api"
            version = details.lastTag ?: "0.0.0"
            additional = ['developers': 'hywenklis.']
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

tasks.jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    maxWarnings = 0
    showViolations = true
    ignoreFailures = false
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile('config/checkstyle/xsl/checkstyle-custom.xsl')
    }
}

checkstyleMain.dependsOn compileJava
checkstyleTest.dependsOn compileTestJava

tasks.withType(SpotBugsTask).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugs {
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
    effort = Effort.MAX
    reportLevel = Confidence.valueOf('HIGH')
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

pmd {
    toolVersion = "7.0.0"
    ignoreFailures = false
    consoleOutput = true
}
